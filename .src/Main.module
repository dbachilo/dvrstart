' Gambas module file

' DVR Software by Dmitry Bachilo
' With Ivan Avdeev's brilliant ideas

Public DVRstorage As String
Public DaysToStore As Integer
Public Cameras As String[]

Public Sub Main()
Dim currentMinutes As String
Dim startupOption As String
Dim daemonize As Boolean
Dim LastRecording As String
Dim FileName As String
Dim camline As String

daemonize = True

For Each startupOption In Application.Args
  If startupOption = "help" Then
    Print "-----------------------------------------------"
    Print "Camera recorder for FreeBSD by Dmitry Bachilo"
    Print "===Options:==="
    Print "version - print version and quit"
    Print "rotate - rotate recordings and quit"
    Print "-d - run in foreground"
    Print "stop - kill currently running ffmpeg and dvrstart"
    Print "-----------------------------------------------"
    Quit
  Endif
  
  If startupOption = "version" Then
    Print Application.Version
    Quit
  Endif
  
  If startupOption = "rotate" Then 
    RotateRecords
    Quit
  Endif
  
  If startupOption = "-d" Then daemonize = False
  
  If startupOption = "stop" Then StopRecord
  
Next

If daemonize = True Then Application.Daemon = True

DVRstorage = Trim(File.Load("/usr/local/etc/dvr/storage.conf"))
DaysToStore = Val(Trim(File.Load("/usr/local/etc/dvr/storedays.conf"))) * -1
Cameras = Split(File.Load("/usr/local/etc/dvr/cams.conf"), "\n")

RotateRecords

StartAllCams

Shell "echo '" & Format(Now, "dd-mm-yyyy hh:nn:ss") & " DVR service started' >> /var/log/dvr.log" Wait

currentMinutes = Format(Time(Now), "nn")

If currentMinutes = "00" Or currentMinutes = "20" Or currentMinutes = "40" Then Goto RunningRoutine

While currentMinutes <> "00" And currentMinutes <> "20" And currentMinutes <> "40"
  Wait 10
  currentMinutes = Format(Time(Now), "nn")
Wend

AjustFilenames

RunningRoutine:

Do

If Exist("/tmp/stopdvr") Then StopRecord

If Format(Time(Now), "hh:nn") = "00:00" Then
  AjustFilenames
  RotateRecords
Endif


If Format(Time(Now), "nn") = "30" Then
  
  For Each camline In Cameras
    For Each FileName In Dir(DVRstorage & Split(camline, ";")[4], "*.mp4").Sort()
      LastRecording = FileName
    Next
      If InStr(LastRecording, Format(Now, "yyyy-mm-dd") & "]-" & Format(Time(Now), "hh")) = 0 Then
        Shell "echo '" & Format(Now, "dd-mm-yyyy hh:nn:ss") & " Camera " & Split(camline, ";")[4] & " has no records in this hour' >> /var/log/dvr.log"
      Endif
  Next

End If

Wait 60

Loop

Catch
  Shell "echo '" & Format(Now, "dd-mm-yyyy hh:nn:ss") & " error occured: " & Error.Text & ", deamon terminated' >> /var/log/dvr.log"
  Quit
End

Public Sub StartAllCams()
Dim camline As String
Dim camSettings As String[]
Dim camUrl As String
Dim Proto As String


For Each camline In Cameras
  camSettings = Split(camline, ";")

  Select Case camSettings[3]
    Case "Axis"
      camUrl = "/axis-media/media.amp"
      Proto = "-i rtsp://"
    Case "Hikvision"
      CamUrl = "/ISAPI/Streaming/Channels/101"
      Proto = "-i rtsp://"
    Case "Trassir"
      CamUrl = "/live/main"
      Proto = "-i rtsp://"
    Case "Cisco"
      CamUrl = ""
      Proto = "-i rtsp://"
    Case "Cisco2421"
      CamUrl = "/img/media.sav"
      Proto = "-i rtsp://"
    Case "Dlink"
      CamUrl = "/live1.sdp"
      Proto = "-i rtsp://"
    Case "DlinkDCS"
      CamUrl = "/play1.sdp"
      Proto = "-i rtsp://"
    Case "Dlink920L"
      CamUrl = "/MJPEG.CGI"
      Proto = "-f mjpeg -i http://"
    Case "Novicam"
      CamUrl = "/Streaming/Channels/101"
      Proto = "-i rtsp://"
  End Select
  
  camUrl = Proto & camSettings[1] & ":" & camSettings[2] & "@" & camSettings[0] & camUrl
  StartRecord(CamUrl, camSettings[4])
Next

Catch
  Return

End

Public Sub StartRecord(CamURl As String, CamName As String)

If Not Exist(DVRstorage & CamName) Then Mkdir DVRstorage & CamName
  
Shell ("while [ true ] ; do  sleep 1 ; ffmpeg " & CamURL & " -vsync 0 -f segment -strftime 1 -reset_timestamps 1 -segment_time 1200 -vcodec copy " & DVRstorage & CamName & "/record-[%F]-%H-%M.mp4 ; done")
  
End



Public Sub AjustFilenames()
  
Shell "killall ffmpeg"
Shell "echo '" & Format(Now, "dd-mm-yyyy hh:nn:ss") & " filenames adjusted' >> /var/log/dvr.log"
  
End

Public Sub RotateRecords()
Dim dirlist As String[]
Dim CamDir As String
Dim DateInQuestion As String

DateInQuestion = "*" & Format(DateAdd(Now, gb.day, DaysToStore), "yyyy-mm-dd") & "*"

dirlist = Dir(DVRstorage, "", gb.Directory)

For Each CamDir In dirlist
  Shell "rm " & DVRstorage & CamDir & "/" & DateInQuestion
Next
  
End

Public Sub StopRecord()

Try Kill "/tmp/stopdvr"
Shell "killall sh" Wait
Shell "killall ffmpeg" Wait
Shell "echo '" & Format(Now, "dd-mm-yyyy hh:nn:ss") & " DVR stopped' >> /var/log/dvr.log"
Shell "killall gbr3" Wait
Shell "killall gbx3" Wait
Quit
  
End

